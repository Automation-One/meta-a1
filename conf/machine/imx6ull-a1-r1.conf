#@TYPE: Machine
#@NAME: Automation One i.MX6ULL Rev. 1 board
#@SOC: i.MX6UL/i.MX6ULL
#@DESCRIPTION: Machine configuration for Automation One i.MX6ULL Revision 1
#@MAINTAINER: O.S. Systems Software <contato@ossystems.com.br>

MACHINEOVERRIDES =. "mx6:mx6ul:"

include conf/machine/include/imx-base.inc
include conf/machine/include/tune-cortexa7.inc

USE_VT = "0"
SERIAL_CONSOLES = "115200;ttymxc3"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-a1"
PREFERRED_PROVIDER_u-boot ?= "u-boot-a1"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-a1"

KERNEL_CLASSES ?= " kernel-fitimage "
KERNEL_IMAGETYPES ?= "fitImage"
KERNEL_DEVICETREE ?= "imx6ull-a1-gw.dtb"

PREFERRED_PROVIDER_u-boot-fw-utils ?= "libubootenv"
PREFERRED_PROVIDER_u-boot-default-script ?= "u-boot-updatehub-script"

UBOOT_CONFIG = "kontron-mx6ul"
UBOOT_CONFIG[kontron-mx6ul] = "kontron_mx6ul_defconfig"

SPL_BINARY = "SPL"
UBOOT_ENTRYPOINT = "0x81000000"
UBOOT_MAKE_TARGET = "u-boot.img SPL"
UBOOT_SUFFIX = "img"

# Deploy only fitimage and dtb files in /boot
RDEPENDS_${KERNEL_PACKAGE_NAME}-base = ""
MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "kernel-image-fitimage"
MACHINE_EXTRA_RRECOMMENDS = "\
    kernel-modules \
    linux-firmware-imx-sdma-imx6q \
"

MACHINE_FEATURES_BACKFILL_remove = "qemu-usermode"
MACHINE_FEATURES = "usbgadget usbhost 3g"

IMAGE_CLASSES += "image_types_512m-ubifs-nand"

## NAND 512MB
MKUBIFS_ARGS = "-F -m 4096 -e 253952 -c 950 -x favor_lzo -X 5"
MKUBIFS_ARGS_INTERNAL = "-F -m 4096 -e 253952 -c 100 -x favor_lzo -X 5"
UBINIZE_ARGS = "-m 4096 -p 262144 -s 4096"
UBI_VOLNAME = "spi-nand0"

# The Wic settings is only for installer-nand image
IMAGE_BOOT_FILES = "boot-installer-nand.scr-${MACHINE};boot.scr"
WKS_FILE_DEPENDS = "u-boot-installer-nand-script"
WKS_FILES ?= "imx-uboot-spl-bootpart.wks.in"

# UpdateHub settings
UPDATEHUB_ACTIVE_INACTIVE_BACKEND = "u-boot"
UPDATEHUB_DEVICE_IDENTITY = "cpuinfo-serial"
UPDATEHUB_FILESYSTEM_SUPPORT = "ubifs"
UPDATEHUB_IMAGE_TYPE = "active/inactive"
UPDATEHUB_INSTALL_MODE = "raw copy ubifs"
UPDATEHUB_DOWNLOAD_DIR = "/tmp/download"
UPDATEHUB_RUNTIME_PACKAGES_append = " u-boot-default-script u-boot-default-env"

UPDATEHUB_BOOTSCRIPT_LOAD_A = "\
ubifsload \${loadaddr} /boot/fitImage; \
imxtract \${loadaddr} fdt@${KERNEL_DEVICETREE} \${fdt_addr_r}; \
imxtract \${loadaddr} kernel@1 \${kernel_addr_r}; \
"
UPDATEHUB_BOOTSCRIPT_LOAD_B = "${UPDATEHUB_BOOTSCRIPT_LOAD_A}"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_A = "ubi part spi-nand0; ubifsmount ubi0:system_a; setenv ubiroot ubi0:system_a"
UPDATEHUB_BOOTSCRIPT_FIND_ROOT_B = "ubi part spi-nand0; ubifsmount ubi0:system_b; setenv ubiroot ubi0:system_b"
UPDATEHUB_BOOTSCRIPT_BOOTARGS = "console=ttymxc3,115200 ubi.mtd=0 root=\${ubiroot} rootfstype=ubifs rw quiet imx2-wdt.timeout=60 imx2-wdt.nowayout panic=60"
UPDATEHUB_BOOTSCRIPT_BOOTCMD = "bootz \${kernel_addr_r} - \${fdt_addr_r}"
